//Modified the wire cap model of bankmux input and dff data input. 2014/11/24.
//Updated bankmux instance and BANK_SEL gate cap model.
//============================================
// Define VDD and VSS
//============================================
VVDD (VDD 0) vsource dc=pvdd
VVSS (VSS 0) vsource dc=0

VBANK_SEL0 (BANK_SEL0 0) vsource type=pulse val0=0 val1=pvdd rise=prise fall=pfall width=pwidth
VBANK_SELB0 (BANK_SELB0 0) vsource type=pulse val0=pvdd val1=0 rise=prise fall=pfall width=pwidth


//Inverter subcircuit
subckt INV OUT IN VDD VSS
parameters i
MP (OUT IN VDD VDD) P_TRANSISTOR width=2*i*wdef length=ldef
MN (OUT IN VSS VSS) N_TRANSISTOR width=i*wdef length=ldef
ends INV

// Library name: yq_IBM130RVT
// Cell name: TBUFX2TF
// View name: schematic
subckt TBUFX2TF A OE VDD VSS Y
    M8 (net28 nmen nmin VDD) P_TRANSISTOR length=ldef width=1.75*wdef
    M6 (nmin A VDD VDD) P_TRANSISTOR length=ldef width=3*wdef
    M0 (VDD OE nmen VDD) P_TRANSISTOR length=ldef width=2*wdef
    M9 (Y nmin VDD VDD) P_TRANSISTOR length=ldef width=8*wdef
    M7 (nmin OE VDD VDD) P_TRANSISTOR length=ldef width=2*wdef
    M5 (nmin OE net28 VSS) N_TRANSISTOR length=ldef width=wdef
    M3 (net28 nmen VSS VSS) N_TRANSISTOR length=ldef width=wdef 
    M1 (nmen OE VSS VSS) N_TRANSISTOR length=ldef width=wdef
    M2 (net28 A VSS VSS) N_TRANSISTOR length=ldef width=2*wdef 
    M4 (Y net28 VSS VSS) N_TRANSISTOR length=ldef width=4*wdef 
ends TBUFX2TF
// End of subcircuit definition.

// Tristate buffer
subckt triinv OUT IN en ep VDD VSS 
parameters wp=wdef lp=ldef wn=wdef ln=ldef
M0 (OUT IN net19 VDD) P_TRANSISTOR width=wdef*2*16 length=ldef 
MP (net19 ep VDD VDD) P_TRANSISTOR width=wdef*2*16 length=ldef
M1 (OUT IN net31 VSS) N_TRANSISTOR width=wdef*16 length=ldef 
MN (net31 en VSS VSS) N_TRANSISTOR width=wdef*16 length=ldef 
ends triinv

//BANK_SEL inverter chain subckt
subckt INVCHAIN_BANK_SEL OUT IN VDD VSS
I0 (X1 IN VDD VSS) INV i=1
I1 (X2 X1 VDD VSS) INV i=4
I2 (X3 X2 VDD VSS) INV i=16
I3 (OUT X3 VDD VSS) INV i=48
ends INVCHAIN_BANK_SEL


IBANK_SEL0 (BANK_SEL_OUT0 BANK_SEL0 VDD VSS) INVCHAIN_BANK_SEL
//IBANK_SELB0 (BANK_SEL_OUTB0 BANK_SELB0 VDD VSS) INVCHAIN_BANK_SEL

// number of columns hardcoded at 4 for intial estimates in ocn script

// Wire capacitance for 2, 4, 8, and 16 banks, assumes banks placed
// side by side with control signals propagating from middle of array
// Also accounting for enable capacitance on WS-1 tristate enables
//Updated gate cap model for using new bankmux unit.
CBANK_SEL0 (BANK_SEL_OUT0 0) capacitor c=numBanks/2*ws*cwl*colMux+wdef*cg*1e6*(ws-1)*6
//CBANK_SELB0 (BANK_SEL_OUTB0 0) capacitor c=numBanks/2*ws*cwl*colMux+wdef*cg*1e6*(ws-1)*32

//Use TBUFX2TF as bankmux   
//Tri_state0 (DATA_OUT0 VSS BANK_SEL_OUT0 BANK_SEL_OUTB0  VDD VSS) triinv
Tri_state0 ( VDD BANK_SEL_OUT0 VDD VSS DATA_OUT0_int) TBUFX2TF
IData_buf (DATA_OUT0 DATA_OUT0_int VDD VSS) INVCHAIN_BANK_SEL
//Wire cap plus additional gate cap of DFF
//wire cap should be c=numBanks*ws*cwl*colMux+2*wdef*cg*1e6
//gate cap of DFF is 4.6*wdef*cg*1e6,modified at 11/22/2014
CDATA2 (DATA_OUT0 0) capacitor c=numBanks/2*ws*cwl*colMux+4.6*wdef*cg*1e6

myOption options pwr=all
ic DATA_OUT0=0 DATA_OUT0_int=0
